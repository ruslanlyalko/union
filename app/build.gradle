apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ruslanlyalko.union"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.1"
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    signingConfigs {
        release {
            def propsFile = file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile file('keystore/keystore0.jks')
                keyAlias = props['keyAlias']
                storePassword = props['storePassword']
                keyPassword = props['keyPassword']
            }
        }
        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date()
            def formattedDate = date.format('dd-MM-yy')
            outputFileName = "UNION_${variant.versionName}_${variant.name}_${formattedDate}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    // Firebase
    implementation('com.google.firebase:firebase-core:16.0.4') {
        exclude group: 'com.android.support'
    }
    implementation('com.google.firebase:firebase-auth:16.0.4') {
        exclude group: 'com.android.support'
    }
    implementation('com.google.firebase:firebase-database:16.0.3') {
        exclude group: 'com.android.support'
    }
    implementation('com.google.firebase:firebase-firestore:17.1.1') {
        exclude group: 'com.android.support'
    }
    implementation('com.google.firebase:firebase-functions:16.1.1') {
        exclude group: 'com.android.support'
    }
    implementation('com.google.firebase:firebase-messaging:17.3.3') {
        exclude group: 'com.android.support'
    }
    // ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // Calligraphy
    implementation "uk.co.chrisjenx:calligraphy:2.3.0"
    // Time Picker
    implementation("com.wdullaer:materialdatetimepicker:3.3.1") {
        exclude group: 'com.android.support'
    }
    // Calendar with events
    implementation("com.github.sundeepk:compact-calendar-view:2.0.2.1") {
        exclude group: 'com.android.support'
    }
    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    //Excel
    implementation('org.apache.poi:poi:3.9') {
        exclude group: 'com.android.support'
    }
    // Rx Permission
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    // Multi Slider
    implementation 'io.apptik.widget:multislider:1.3'
    // Scroll
    implementation 'me.everything:overscroll-decor-android:1.0.4'
//    // Glide
//    implementation 'com.github.bumptech.glide:glide:4.7.1'
//    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

}
apply plugin: 'com.google.gms.google-services'
